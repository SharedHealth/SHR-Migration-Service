group 'shr-migration-service'

apply plugin: 'java'
apply plugin: 'rpm'
apply plugin: 'idea'
apply plugin: 'checkstyle'
apply plugin: 'pmd'

def mainClassName = "org.sharedhealth.migrationservice.launch.Main"

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    flatDir {
        dirs 'lib'
    }
}

jar {
    baseName = 'shr-migration-service'
    manifest {
        attributes('Main-Class': mainClassName)
    }
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
    into('lib') {
        from 'libs'
    }
}

ext.libraries = [
        apache_log4j_core    : "org.apache.logging.log4j:log4j-core:2.6.2",
        atomfeed_client      : "org.ict4h:atomfeed-client:1.9.3",
        atomfeed_commons     : "org.ict4h:atomfeed-commons:1.9.3",
        spring_framework     : "org.springframework:spring-core:4.1.1.RELEASE",
        spring_transaction   : "org.springframework:spring-tx:4.1.1.RELEASE",
        spring_boot_logging  : "org.springframework.boot:spring-boot-starter-log4j:1.1.1.RELEASE",
        spring_jdbc          : "org.springframework:spring-jdbc:4.1.1.RELEASE",
        quartz_scheduler     : "org.quartz-scheduler:quartz:1.7.3",
        spring_ctx_support   : "org.springframework:spring-context-support:4.1.1.RELEASE",
        jackson              : "com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.4.0",
        mysql_connector      : "mysql:mysql-connector-java:5.1.6",
        apache_httpclient    : "org.apache.httpcomponents:httpclient:4.3.5",
        apache_commons_codec : "commons-codec:commons-codec:1.9",
        apache_commons_dbcp  : "commons-dbcp:commons-dbcp:1.4",
        apache_commons_io    : "org.apache.commons:commons-io:1.3.2",
        liquibase            : "org.liquibase:liquibase-core:3.2.2",
        hapi_fhir_structures3: "ca.uhn.hapi.fhir:hapi-fhir-structures-dstu3:2.3",
        hapi_fhir_structures2: "ca.uhn.hapi.fhir:hapi-fhir-structures-dstu2:2.3",
        spring_data_cassandra : "org.springframework.data:spring-data-cassandra:1.2.+",
        hector_core          : 'me.prettyprint:hector-core:1.0-5'
]

ext.testLibraries = [
        cassandra_unit       : "org.cassandraunit:cassandra-unit-spring:2.0.2.2",
        h2_database          : "com.h2database:h2:1.4.182",
        mutagen              : "com.toddfast.mutagen:mutagen:0.3.0",
        mutagen_cassandra    : "com.toddfast.mutagen:mutagen-cassandra:0.4.2-SNAPSHOT",
        junit                : "junit:junit:4.12",
        mockito              : "org.mockito:mockito-all:1.10.19",
        wiremock             : "com.github.tomakehurst:wiremock:1.56",
        spring_test          : "org.springframework:spring-test:4.1.1.RELEASE",
]

dependencies {
    compile('org.springframework.boot:spring-boot-starter-web:1.1.8.RELEASE') {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    }
    compile files('lib/org.hl7.fhir.igpublisher.jar')

    compile libraries.values()

    testCompile testLibraries.values()
    compile files('lib/mutagen-cassandra-0.4.2-SNAPSHOT.jar')
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.12'
}

checkstyle {
    configFile rootProject.file('config/checkstyle/checkstyle.xml')
}

pmd {
    toolVersion = '5.0.5'
    ruleSetFiles = files("${project.projectDir}/config/pmd/rulesets.xml")
}

def exportEnvironmentVariablesToXML(defaults) {
    defaults.remove(defaults.envs)
    Properties environmentVariables = new Properties();
    environmentVariables.load(new FileInputStream(new File('env/local.properties')))
    defaults.append new NodeBuilder().envs() {
        environmentVariables.each { prop, value ->
            env(name: prop, value: value)
        }
    }
}

def localEnv = new Properties();
localEnv.load(new FileInputStream(new File('src/test/resources/test.properties')))

private Properties loadConfig() {
    Properties properties = new Properties()
    properties.load(new FileInputStream(new File('env/local.properties')));
    properties
}

task assembly(dependsOn: 'build') << {
    new File("${buildDir}/etc/").mkdir();
}


task exportProperties(dependsOn: 'assembly') << {
    PrintStream output = new PrintStream(new FileOutputStream(file("${buildDir}/etc/shr-migration-service")));
    Properties properties = loadConfig()
    properties.each { prop, val ->
        output.println("export " + prop + "=" + val)
    }
}

task dist(dependsOn: 'exportProperties', type: Rpm) {
    packageName = 'shr-migration-service'
    version = '1.0'
    release = project.hasProperty('release') ? project.release : '1'
    arch = NOARCH
    os = LINUX

    into '/opt/shr-migration-service'

    postInstall = file('scripts/utils/postInstall.sh')
    preUninstall = file('scripts/utils/preUninstall.sh')
    postUninstall = file('scripts/utils/postUninstall.sh')

    from("${buildDir}/etc") {
        fileMode = 0755
        into 'etc'
    }

    from("scripts/rpm") {
        fileMode = 0755
        exclude 'placeholder'
        into 'bin'
    }

    from("${buildDir}/libs") {
        fileMode = 0755
        into 'lib'
    }

    from("scripts/rpm") {
        fileMode = 0755
        exclude 'shr-migration-service'
        exclude 'start.sh'
        into 'var'
    }
}

test {
    environment localEnv
}

task runSHRMigration(type: JavaExec) {
    Properties properties = new Properties();
    properties.load(new FileInputStream(new File('env/local.properties')))
    main = mainClassName
    classpath = sourceSets.main.runtimeClasspath
    environment properties
}

task debugSHRMigration(type: JavaExec) {
    Properties properties = new Properties();
    properties.load(new FileInputStream(new File('env/local.properties')))
    main = mainClassName
    classpath = sourceSets.main.runtimeClasspath
    environment properties
    debug = true
}

idea {
    workspace {
        iws.withXml { xmlFile ->
            def runManager = xmlFile.asNode().component.find { it.@name == 'RunManager' }
            def junitDefaults = runManager.configuration.find { it.@default == 'true' && it.@type == 'JUnit' }
            junitDefaults.remove(junitDefaults.envs)
            junitDefaults.append new NodeBuilder().envs() {
                env(name: 'PATH_TO_CONFIG', value: "\$PROJECT_DIR\$/env/local.properties")
            }
            /*Configuring application runtime parameters*/
            exportEnvironmentVariablesToXML(runManager.configuration.find {
                it.@default == 'true' && it.@type == 'Application'
            });
        }
    }
    project {
        jdkName = '1.8'
        languageLevel = '1.8'
    }
}

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath(
                'com.netflix.nebula:gradle-ospackage-plugin:2.+'
        )
    }
}
